# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

zmodload zsh/complist

source "$HOME/.zsh/zsh-defer/zsh-defer.plugin.zsh"
zsh-defer source "$HOME/.zsh/zsh-autosuggestions/zsh-autosuggestions.zsh"
zsh-defer source "$HOME/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
# https://github.com/jeffreytse/zsh-vi-mode
source "$HOME/.zsh/zsh-vi-mode/zsh-vi-mode.plugin.zsh"

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  zsh-defer source $file
done

unset config_files
